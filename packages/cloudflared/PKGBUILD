# Maintainer: MedzikUser <nivua1fn@duck.com>
pkgname='cloudflared'
pkgver="2023.1.0"
pkgrel=1
pkgdesc='Cloudflare Tunnel client (formerly Argo Tunnel)'
arch=('x86_64')
url="https://github.com/cloudflare/cloudflared"
license=('Apache')
depends=('glibc')
makedepends=('go')
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=('68b66ec333329cda386b304a333b6f24e3ecc09696184603ca37426d522c4cd2')

build() {
  cd "${pkgname}-${pkgver}"

  local _flags
  _flags=(
    -X main.Version="$pkgver"
    -X main.BuildTime="$(date -u -d "@${SOURCE_DATE_EPOCH}" +'%FT%TZ')"
  )

  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -modcacherw"

  go build \
    -v \
    -ldflags "${_flags[*]}" \
    -o bin/$pkgname \
    ./cmd/$pkgname
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 bin/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
