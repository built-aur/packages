# Maintainer: MedzikUser <nivua1fn@duck.com>
_npmname='@angular/cli'
pkgname='angular-cli'
pkgver="15.2.6"
pkgrel=1
pkgdesc='CLI tool for Angular'
arch=('any')
url="https://www.npmjs.com/package/${_npmname}"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
_file="cli-${pkgver}.tgz"
source=("https://registry.npmjs.org/${_npmname}/-/${_file}")
noextract=("${_file}")
sha256sums=('5b5b4122499fbb3aa8ca88e53d4bd226997c9dc170c9a366154e04704a934b6c')

package() {
  npm install -g --prefix "${pkgdir}/usr" "${_file}"

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "${pkgdir}"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "${pkgdir}"
}
